apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "28.0.3"

    publishNonDefault true

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 20400
        versionName "2.4.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            moduleName "LuaScriptCore-Unity-Android"
            stl "c++_static"
            ldLibs "log"
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"      //筛选只需要的架构，其中armeabi、mips、mips64已标记过时
        }

        externalNativeBuild {
            ndkBuild {

                arguments "APP_STL=c++_static",
                        "NDK_DEBUG=1",
                        "APP_PLATFORM=android-16",
                        "NDK_TOOLCHAIN_VERSION=clang"
                cppFlags "-frtti",
                        "-fexceptions",
                        "-std=c++11",
                        "-g",
                        "-Wno-deprecated-declarations",
                        "-fpermissive"

            }
        }

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }
    }

    sourceSets{

        main{

            jni.srcDirs = ["src/main/jni", "../../../lua-common", "../../../lua-core/src", "../../UnityCommon"];

        }
    }

    externalNativeBuild {

        ndkBuild {
            path "src/main/jni/Android.mk"
        }

    }

    //解决过期架构过期问题
    task ndkClean(type: Delete) {
        // remove unused archs from build cache
        delete fileTree('.externalNativeBuild') {
            exclude defaultConfig.ndk.abiFilters.collect { '**/' + it }
        }
    }
    tasks.findByPath(':clean').dependsOn ndkClean
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'
}
