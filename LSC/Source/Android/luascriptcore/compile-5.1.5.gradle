apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'
    publishNonDefault true

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 20400
        versionName "2.4.0"

        ndk {
            moduleName "LuaScriptCore"
            stl "c++_static"
            ldLibs "log"
            abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"      //筛选只需要的架构，其中armeabi、mips、mips64已标记过时
        }

        externalNativeBuild {

            ndkBuild {

                arguments "APP_STL=c++_static",
                        "NDK_DEBUG=1",
                        "APP_PLATFORM=android-16",
                        "NDK_TOOLCHAIN_VERSION=clang"

                cppFlags "-frtti",
                        "-fexceptions",
                        "-std=c++11",
                        "-g",
                        "-Wno-deprecated-declarations",
                        "-fpermissive"

            }

        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            jniDebuggable true
        }

    }

    sourceSets {
        main {

            jniLibs.srcDirs = []
            jni.srcDirs "src/main/jni", "../../lua-core-5.1.5/src", "../../lua-common"

        }
    }

    externalNativeBuild {

        ndkBuild {
            path "src/main/jni/Android-5.1.5.mk"
        }

    }

    //解决过期架构过期问题
    task ndkClean(type: Delete) {
        // remove unused archs from build cache
        delete fileTree('.externalNativeBuild') {
            exclude defaultConfig.ndk.abiFilters.collect { '**/' + it }
        }
    }
    tasks.findByPath(':clean').dependsOn ndkClean
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'LuaScriptCore'

    publishedGroupId = 'com.github.vimfung.luascriptcore'
    libraryName = 'LuaScriptCore'
    artifact = 'luascriptcore'

    libraryDescription = 'An easy-to-use Android-Lua bridge'

    siteUrl = 'https://github.com/vimfung/LuaScriptCore'
    gitUrl = 'https://github.com/vimfung/LuaScriptCore.git'

    libraryVersion = '2.4.0'

    developerId = 'vimfung'
    developerName = 'Vim Fung'
    developerEmail = 'vimfung@qq.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
}

apply from: './installv1.gradle'
apply from: './bintrayv1.gradle'